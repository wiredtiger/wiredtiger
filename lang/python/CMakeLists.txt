#
# Public Domain 2014-present MongoDB, Inc.
# Public Domain 2008-2014 WiredTiger, Inc.
#  All rights reserved.
#
# See the file LICENSE for redistribution information.
#

project(python_swig C)

# UseSWIG: Policy option to use the standard target name over a generated name.
if(POLICY CMP0078)
    cmake_policy(SET CMP0078 NEW)
endif()

# UseSWIG: Policy option to honor SWIG_MODULE_NAME.
if(POLICY CMP0086)
    cmake_policy(SET CMP0086 NEW)
endif()

find_package(SWIG REQUIRED)
include(${SWIG_USE_FILE})

set(python_libs)
set(python_version)
if("${CMAKE_VERSION}" VERSION_LESS "3.12.0")
    # This method of finding python libs has been deprecated since version 3.12.
    # If we are running with a greater CMake version, opt to use the Python3 package.
    set(Python_ADDITIONAL_VERSIONS 3.9 3.8 3.7 3.6 3.5)
    find_package(PythonInterp REQUIRED)
    find_package(PythonLibs REQUIRED)
    include_directories(${PYTHON_INCLUDE_DIRS})
    set(python_libs ${PYTHON_LIBRARIES})
    set(python_version ${PYTHON_VERSION_STRING})
else()
    find_package(Python3 COMPONENTS Interpreter Development REQUIRED)
    include_directories(${Python3_INCLUDE_DIRS})
    set(python_libs ${Python3_LIBRARIES})
    set(python_version ${Python3_VERSION})
endif()

# Manually specify the interface name '_wiredtiger' over a generated name (for backwards compatibility).
list(APPEND swig_flags "-interface;_wiredtiger")

list(APPEND swig_flags "-DPY_MAJOR_VERSION=${VERSION_MAJOR}")
list(APPEND swig_flags "-threads")
list(APPEND swig_flags "-O")
list(APPEND swig_flags "-nodefaultctor")
list(APPEND swig_flags "-nodefaultdtor")

# Pass in our generated include directories.
list(APPEND swig_flags "-I${CMAKE_BINARY_DIR}/include")
list(APPEND swig_flags "-I${CMAKE_BINARY_DIR}/config")

set(CMAKE_SWIG_FLAGS ${swig_flags})

swig_add_library(_wiredtiger
  TYPE SHARED
  LANGUAGE python
  SOURCES ${CMAKE_CURRENT_LIST_DIR}/wiredtiger.i
)

# Capture the target name generated by 'swig_add_library'. On older versions of cmake (< 3.13),
# CMP0078 policy is default set to OLD (leading to a potentially different target name).
set(swig_wt_target ${SWIG_MODULE__wiredtiger_REAL_NAME})

# Depending on the chosen compiler (clang or gcc), a slightly different set of compiler flags
# will be used.
set(swig_c_flags)
if("${CMAKE_C_COMPILER_ID}" STREQUAL "Clang" OR "${CMAKE_C_COMPILER_ID}" STREQUAL "AppleClang")
    set(swig_c_flags
        -Wno-pointer-sign
        -Wno-ignored-qualifiers
        -Wno-incompatible-pointer-types
        -Wno-int-conversion
        -Wno-sign-conversion
    )
else()
    set(swig_c_flags
        -Wno-discarded-qualifiers
        -Wno-incompatible-pointer-types
        -Wno-int-conversion
        -Wno-sign-conversion
    )
endif()
list(APPEND swig_c_flags -I${CMAKE_SOURCE_DIR})
list(APPEND swig_c_flags -I${CMAKE_BINARY_DIR}/config)

# Older versions of SWIG (<= 3) will generate deprecated python3 buffer protocols (being marked as
# deprecated in Python 3.7+). Supress these warnings if we end up with an older SWIG compiler
# and newer python3 version.
if((${SWIG_VERSION} VERSION_LESS 4) AND (${python_version} VERSION_GREATER_EQUAL 3.7))
    list(APPEND swig_c_flags -Wno-deprecated-declarations)
endif()

target_compile_options(${swig_wt_target}
    PRIVATE ${swig_c_flags}
)

swig_link_libraries(_wiredtiger wiredtiger ${python_libs})

# Setup our output 'wiredtiger' directory to hold the python module sources.
file(COPY ${CMAKE_CURRENT_SOURCE_DIR}/wiredtiger
    DESTINATION ${CMAKE_CURRENT_BINARY_DIR})

# Setup the module constructor file (python requirement).
add_custom_command(TARGET ${swig_wt_target}
    POST_BUILD
    COMMAND ${CMAKE_COMMAND} -E copy
        ${CMAKE_CURRENT_SOURCE_DIR}/wiredtiger/init.py
        ${CMAKE_CURRENT_BINARY_DIR}/wiredtiger/__init__.py
)

# The generated swig file cannot be named 'wiredtiger.py' in the target directory
# as we won't be able to import it.
add_custom_command(TARGET ${swig_wt_target}
    POST_BUILD
    COMMAND ${CMAKE_COMMAND} -E copy
        ${CMAKE_CURRENT_BINARY_DIR}/wiredtiger.py
        ${CMAKE_CURRENT_BINARY_DIR}/wiredtiger/swig_wiredtiger.py
)

# `swig_add_library` will end up prepending an extra '_' to our target name, resulting in
# an output the  file '__wiredtiger.so'. We are unable to manually specify the output file
# name.
add_custom_command(TARGET ${swig_wt_target}
    POST_BUILD
    COMMAND ${CMAKE_COMMAND} -E rename
        $<TARGET_FILE:${swig_wt_target}>
        $<TARGET_FILE_DIR:${swig_wt_target}>/_wiredtiger.so
)
